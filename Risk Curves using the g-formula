* Create a folder in which the simulated datasets will be saved will be saved
global data = "C:\Users\micha\Desktop\Papers\New\Estimate change in a risk factor\Simulation\Final\Results"

* Open the simulated dataset from github
use "C:\Users\micha\Desktop\Papers\New\Estimate change in a risk factor\Simulation\Final\Results\Datasets\Final dataset (pooled)_1.dta", replace


global outcome_model_rc3= "weight_loss weight_loss#c.t weight_loss#c.t#c.t weight_loss#c.t#c.t#c.t weight_gain weight_gain#c.t weight_gain#c.t#c.t weight_gain#c.t#c.t#c.t   c.t c.t#c.t c.t#c.t#c.t i.sm_status0 sex age diuretics0 diuretics1 fh_CVD  BMI0 sm_cess1"



drop if CVD1==1


*** Calculation of the risk curves

logistic CVD_pooled $outcome_model_rc3, cluster(id)

* Create a dataset with all time points for each treatment
* Re-expand data for all treatments
keep if t==0

expand 9 if t==0

bysort id: replace t=2*(_n-1)

* Create 3 copies of each subjects, one for each treatment
expand 3

sort id t

by id t: gen intervention=_n

replace weight_loss=0
replace weight_loss=1 if intervention==1

replace weight_gain=0
replace weight_gain=1 if intervention==3


sort id intervention t

* Generate the probability of developing the event of interest "p_event_k" and the probability of remaining disease free "p_survival_k", which is 1-"p_event_k"
predict p_event_k, pr // estimated propability of the event at each time point k
gen     p_survival_k=1-p_event_k // estimated propability of remaining disease-free at each time point k

* The probability of remaining disease-free at time k is the product of the probabilities of remaining disease-free up to time t
sort id intervention t
gen p_survival=p_survival_k if t==0
bysort id intervention: replace p_survival=p_survival_k*p_survival[_n-1] if t>0


* The probability of the developing the event at time k is one minus the probability of remaining disease-free at time k
gen p_risk=1-p_survival
by intervention, sort: summarize p_risk if t==16
sort intervention t id
by intervention t: egen p_mean_risk=mean(p_risk)

* We want to generate the graph of standardised risk curves over time, undr the 3 interventions
* We need to average the adjusted risk curves across all values
* Important note: We add two time points (time=0 & time=1), so that the risk curves begin with p_risk(time==0)=0 & p_risk(time==1)=0
expand 3 if t==0, generate(newtime)
recode newtime 1=-1
sort id intervention t newtime

replace p_mean_risk=0 if newtime==-1 & t==0
gen     time2=t+4
replace time2=0 if newtime==-1 & t==0
replace time2=2 if time2[_n-1]==0

replace p_mean_risk=p_mean_risk*100
separate p_mean_risk, by(intervention)

keep if _n<=3*11
keep p_mean_risk1 p_mean_risk2 p_mean_risk3 time2 

twoway (line p_mean_risk1 time2, sort) (line p_mean_risk2 time2, sort) (line p_mean_risk3 time2, sort), ylabel(0(2)12) xlabel(0(2)20) ytitle("Risk (%)") xtitle("Years of follow-up") legend(label(1 "Weight Loss") label(2 "Weight Maintenance") label(3 "Weight Gain"))  

